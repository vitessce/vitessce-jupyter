include: "../common.smk"
configfile: "config.yml"

# TODO: start from the real raw files
BASE_URL = "https://s3.amazonaws.com/vitessce-data/0.0.31/master_release/wang/wang"


rule all:
    input:
        [ (PROCESSED_DIR / f) for f in config['output'] ]

# Convert the old JSON files to simpler CSV and JSON formats.
rule convert_to_csv:
    input:
        cells=(RAW_DIR / "cells.json"),
        molecules=(RAW_DIR / "molecules.json"),
        expression_matrix=(RAW_DIR / "genes.json")
    output:
        cells=(PROCESSED_DIR / "wang_2018_scientific_reports.cells.csv"),
        segmentations=(PROCESSED_DIR / "wang_2018_scientific_reports.cells.segmentations.json"),
        matrix=(PROCESSED_DIR / "wang_2018_scientific_reports.cells.matrix.csv"),
        molecules=(PROCESSED_DIR / "wang_2018_scientific_reports.molecules.csv")
    params:
        script=(SRC_DIR / "convert_to_csv.py")
    shell:
        '''
        python {params.script} \
            -ic {input.cells} \
            -im {input.molecules} \
            -iem {input.expression_matrix} \
            -oc {output.cells} \
            -ocs {output.segmentations} \
            -ocm {output.matrix} \
            -om {output.molecules}
        '''

# Download original JSON files.
rule download_file:
    output:
        (RAW_DIR / "{prefix}.json")
    shell:
        '''
        curl -L -o {output} {BASE_URL}.{wildcards.prefix}.json
        '''